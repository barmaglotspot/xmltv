#!/usr/bin/perl -w

eval 'exec /usr/bin/perl -w -S $0 ${1+"$@"}'
    if 0; # not running under some shell

=pod

=head1 NAME

tv_grab_ar - Grab TV listings for Argentina.

=head1 SYNOPSIS

tv_grab_ar --help

tv_grab_ar [--config-file FILE] --configure [--gui OPTION]

tv_grab_ar [--config-file FILE] [--output FILE] [--days N]
           [--offset N] [--quiet] [--GetDetails]

tv_grab_ar --list-channels

tv_grab_ar --capabilities

tv_grab_ar --version

=head1 DESCRIPTION

Output TV listings for several channels available in Argentina.
Now supports the terrestrial analog tv listings, which is the most common tv 
viewed in Argentina. 
The tv listings comes from http://www.buscadorcablevision.com.ar/
The grabber relies on parsing HTML so it might stop working at any time.

First run B<tv_grab_ar --configure> to choose, which channels you want
to download. Then running B<tv_grab_ar> with no arguments will output
listings in XML format to standard output.

B<--configure> Prompt for which channels,
and write the configuration file.

B<--config-file FILE> Set the name of the configuration file, the
default is B<~/.xmltv/tv_grab_ar.conf>.  This is the file written by
B<--configure> and read when grabbing.

B<--gui OPTION> Use this option to enable a graphical interface to be used.
OPTION may be 'Tk', or left blank for the best available choice.
Additional allowed values of OPTION are 'Term' for normal terminal output
(default) and 'TermNoProgressBar' to disable the use of XMLTV::ProgressBar.

B<--output FILE> Write to FILE rather than standard output.

B<--days N> Grab N days.  The default is 3.

B<--offset N> Start N days in the future.  The default is to start
from today.

B<--quiet> Suppress the progress messages normally written to standard
error.

B<--capabilities> Show which capabilities the grabber supports. For more
information, see L<http://wiki.xmltv.org/index.php/XmltvCapabilities>

B<--version> Show the version of the grabber.

B<--help> Print a help message and exit.

=head1 SEE ALSO

L<xmltv(5)>.

=head1 AUTHOR
1.10 Mariano S. Cosentino, Mok@marianok.com.ar
1.9 Mariano S. Cosentino, Mok@marianok.com.ar
1.8 rmeden
1.7 Mariano S. Cosentino, Mok@marianok.com.ar
1.6 Mariano S. Cosentino, Mok@marianok.com.ar
1.5 Christian A. Rodriguez, car@cespi.unlp.edu.ar, based on tv_grab_es, from Ramon Roca.

=head1 BUGS

This grabber extracts all information from cablevision website. Any change in this
web page may cause this grabber to stop working.
Retrieving the description information adds a considerable amount of time to the run and makes the file quite large.
migth be a good idea to add a cache file the keeps the program description so there is no need to fetch it.

=cut



# Author's Updates
#   1.10 	Mariano Cosentino
#		* 	fixed prototype warning
#		* 	Removed default value for location, as the old channel ids are no compatible with the site's new format.
# Author's Updates
#   1.9 	Mariano Cosentino
#		* 	The source website changed, I've adapted the script to the new format
#		*	Additional information is now handled directly here, no need for most of tv_extractinfo_ar
#		*	Added support for locations. 
#		*	*	Added parameter --zone for Zone ovewride w/o running full config
#		*	*	TELECENTRO is handled as a zone.
#		*	*	DirectTV is handled as a zone. *** in any case, if you have DTV, you should be using TV_GRAB_DTV_LA instead of this grabber :-) ***
#		*	*	All other Zones are extracted automatically from the CV website and all this zones have Cablevision's programming (not Telecentro, no DTV)
#		*	*	The "default" Zone is still Capital Federal / Gran Buenos Aires (Cablevision zone 3)
#		*	*	This is still to be tested by people outside GBA to see if is acurate. 
#		*	*	This is based on the work of Mauro Meloni and his Phyton version of TV_GRAB_AR http://mauromeloni.com.ar/files/tv_grab_ar.py.
#		*	Added parameter --getdetails to allow the user to control if they want the full details (time consuming) *** I should probably put it in the config section ***
#		* 	While it's working fine, this version still has a lot of room to improvements. I decided to release a quick-and-dirty version in order to have it up and running and then I wil work on the enhacements.
#		*	Some additional error handling should avoid fatal failures when the Cablevision  website becomes unresponsive (far too often for my taste).
#   1.8		rmeden
#		apply dekarl's patch 3057655 to update lots of xmltv.org links
#   1.7 	Mariano Cosentino
#		* 	The source website changed, I've adapted the script to the new format
#		*	Enhaced the handling of latin chars.
#		*	Added control and cache to avoid downloading the same program data multiple times in the same session.
#   1.6 	Mariano Cosentino
#		* 	Added descriptions support
#		*	All properties (director, autor, etc) are appended to the description for added value
#		*	All times are now UTC as per XMLTV recomendations
#		*	Cablevision and Multicanal use the same format (both are the same company now) so added variable to customize where to grab the listings from.



# Author's TODOs & thoughts
#
# Add channel logo support (download from $urlRoot/logos/lg_99.png where 99 is the channel id)
# Add Support for Argentinean DST
# add properties in the corresponding XMLTV fields without relying on the tv_extractinfo_ar (partialy done)
# migth be a good idea to add a cache file the keeps the program description so there is no need to fetch it.


my $CableCompany = "cablevision"; #"multicanal";
my $urlRoot="http://www.buscador" . $CableCompany . ".com.ar/";


######################################################################
# initializations

use strict;
use XMLTV::Version '$Id: tv_grab_ar,v 1.12 2011/06/22 05:56:34 rmeden Exp $ ';
use XMLTV::Capabilities qw/baseline manualconfig cache/;
use XMLTV::Description 'Argentina';
use Getopt::Long;
use Date::Manip;
use HTML::TreeBuilder;
use HTML::Entities; # parse entities
use IO::File;

use Encode qw(from_to is_utf8 _utf8_off encode);
use utf8;

use XMLTV;
use XMLTV::Memoize;
use XMLTV::ProgressBar;
use XMLTV::Ask;
use XMLTV::Config_file;
use XMLTV::DST;
# So we are not affected by winter/summer timezone
$XMLTV::DST::Mode='none';

#use XMLTV::TZ 'UTC'; # qw(parse_local_date);
Date_Init('TZ=UTC');
use XMLTV::Get_nice;
use XMLTV::Mode;
use XMLTV::Date;


sub select_Location();




# Todo: perhaps we should internationalize messages and docs?
use XMLTV::Usage <<END
$0: get Argentinian television listings in XMLTV format
To configure: $0 --configure [--config-file FILE]
To grab listings: $0 [--config-file FILE] [--output FILE] [--days N]
        [--offset N] [--quiet] [--getdetails] [--zone N]
To list channels: $0 --list-channels
To show capabilities: $0 --capabilities
To show version: $0 --version
END
  ;
# We don't need random delays
$XMLTV::Get_nice::Delay=0;

# Attributes of the root element in output.
my $HEAD = { 'source-info-url'     => $urlRoot,
	     'source-data-url'     => $urlRoot,
	     'generator-info-name' => 'XMLTV',
	     'generator-info-url'  => 'http://xmltv.org/',
	   };
		   
# default language
my $LANG="es";

# Selected location
our %location;

# Global channel_data
our @ch_all;

# Global ProgID/Description Hash
my %Descriptions = ();

# Overlaping time threshold allowed in seconds
our $allowed_overlap_tresh=180;


my %channels;
my @channels;

######################################################################
# get options

# Get options, including undocumented --cache option.
XMLTV::Memoize::check_argv('XMLTV::Get_nice::get_nice_aux');
my ($opt_days, $opt_offset, $opt_help, $opt_output,
    $opt_configure, $opt_config_file, $opt_gui,
    $opt_quiet, $opt_get_details, $opt_zone, $opt_list_channels);
$opt_days  = 3; # default
$opt_offset = 0; # default
$opt_quiet  = 0; # default
$opt_zone = 0; # default for 
$opt_get_details = 0; # default

GetOptions('days=i'        => \$opt_days,
	   'offset=i'      => \$opt_offset,
	   'help'          => \$opt_help,
	   'configure'     => \$opt_configure,
	   'config-file=s' => \$opt_config_file,
       'gui:s'         => \$opt_gui,
	   'output=s'      => \$opt_output,
	   'quiet'         => \$opt_quiet,
	   'getdetails'	   => \$opt_get_details,
	   'zone=i'        => \$opt_zone,
	   'list-channels' => \$opt_list_channels
	  )
  or usage(0);
die 'number of days must not be negative'
  if (defined $opt_days && $opt_days < 0);
usage(1) if $opt_help;

XMLTV::Ask::init($opt_gui);

my $mode = XMLTV::Mode::mode('grab', # default
			     $opt_configure => 'configure',
			     $opt_list_channels => 'list-channels',
			    );

# File that stores which channels to download.
my $config_file
  = XMLTV::Config_file::filename($opt_config_file, 'tv_grab_ar', $opt_quiet);

my @config_lines; # used only in grab mode
if ($mode eq 'configure') {
    XMLTV::Config_file::check_no_overwrite($config_file);
}
elsif ($mode eq 'grab') {
    @config_lines = XMLTV::Config_file::read_lines($config_file);
}
elsif ($mode eq 'list-channels') {
    # Config file not used.
}
else { die }

if (($opt_zone != 0) && ($mode ne 'configure')) {
	warn "Zone overwride by user, using zone $opt_zone\n";
	%location = (
		id => $opt_zone,
		name => 'USER SELECTED LOCATION'
			);
}
######################################################################
# write configurationDateCalc(

if ($mode eq 'configure') {
    open(CONF, ">$config_file") or die "cannot write to $config_file: $!";

	%location = select_Location();
	print CONF "location $location{id} $location{name}\n";
	
	# Whatever we are doing, we need the channels data.
	%channels = get_channels(); # sets @ch_all
	

    # Ask about each channel.
    my @chs = sort keys %channels;
    my @names = map { $channels{$_} } @chs;
    my @qs = map { "add channel $_?" } @names;
    my @want = ask_many_boolean(1, @qs);
    foreach (@chs) {
        my $w = shift @want;
        warn("cannot read input, stopping channel questions"), last
          if not defined $w;
        # No need to print to user - XMLTV::Ask is verbose enough.

        # Print a config line, but comment it out if channel not wanted.
        print CONF '#' if not $w;
        my $name = shift @names;
        print CONF "channel $_ $name\n";
        # TODO don't store display-name in config file.
    }

    close CONF or warn "cannot close $config_file: $!";
    say("Finished configuration.");

    exit();
}



# Not configuration, we must be writing something, either full
# listings or just channels.
#
die if $mode ne 'grab' and $mode ne 'list-channels';

# Options to be used for XMLTV::Writer.
my %w_args;
if (defined $opt_output) {
    my $fh = new IO::File(">$opt_output");
    die "cannot write to $opt_output: $!" if not defined $fh;
    $w_args{OUTPUT} = $fh;
}
$w_args{encoding} = 'utf-8';
my $writer = new XMLTV::Writer(%w_args);
$writer->start($HEAD);

if ($mode eq 'list-channels') {
    $writer->write_channel($_) foreach @ch_all;
    $writer->end();
    exit();
}

######################################################################
# We are producing full listings.
die if $mode ne 'grab';

# Read configuration
my $line_num = 1;
foreach (@config_lines) {
    ++ $line_num;
    next if not defined;
if (/^location:?\s+(\S+)\s+([^\#]+)/) {
        %location=( id => $1, name=>$2 );      
    }else{
	die "No location specified, run me with --configure\n"
  	if not keys %location;
#	    if (/^channel:?\s+(\S+)\s+(\S+)\s+([^\#]+)/) {
	# Whatever we are doing, we need the channels data.
	%channels = get_channels() if not  %channels; # sets @ch_all


 	   if (/^channel:?\s+(\S+)\s+([^\#]+)/) {
		my $ch_did = $1;
#		my $ch_sourceid = $2;
		my $ch_name = $2;
		$ch_name =~ s/\s*$//;
		push @channels, $ch_did;
		$channels{$ch_did} = $ch_name;
	    }
	    else {
		warn "$config_file:$line_num: bad line\n";
	    }
	}
}


# Whatever we are doing, we need the channels data.
#%channels = get_channels(); # sets @ch_all



######################################################################
# begin main program

# Assume the listings source uses ARG TIME (see BUGS above).
my $now = DateCalc(parse_date('now'), "$opt_offset days");
die "No channels specified, run me with --configure\n"
  if not keys %channels;


die "No location specified, run me with --configure\n"
  if not keys %location;


#if (not keys %location) {
#	warn "No location specified, will use 'Capital Federal -GBA' by default. If you wish a diferent one run me with --configure\n" ;
#	%location = (
#			id => 3,
#			name => 'Capital Federal -GBA'
#		);
#}
my @to_get;


foreach my $ch_did (@channels) {
    my $index=0;
    my $ch_name=$channels{$ch_did};
    my $ch_xid="$ch_did.cablevision";
    my $tot_ch=@ch_all;
    while (($tot_ch > $index) and (${$ch_all[$index]}{'id'} ne $ch_xid)) {
        $index=$index+1;
    }
    next if ($tot_ch <= $index);
    my $ch_num=${ch_all[$index]}{'channel-num'};
    $writer->write_channel({ id => $ch_xid,
			     'display-name' => [ [ $ch_name ],
						 [ $ch_num ] ] });
    my $day=UnixDate($now,'%Q');
    for (my $i=0;$i<$opt_days;$i++) {
        push @to_get, [ $day, $ch_did, $ch_num ];
        #for each day
        $day=nextday($day); die if not defined $day;
    }
}


# This progress bar is for both downloading and parsing.  Maybe
# they could be separate.
#
my $bar = new XMLTV::ProgressBar('getting listings', scalar @to_get)
  if not $opt_quiet;
foreach (@to_get) {
	foreach (process_table($_->[0], $_->[1], $_->[2])) {
		$writer->write_programme($_);
	}
	update $bar if not $opt_quiet;
}
$bar->finish() if not $opt_quiet;
$writer->end();

######################################################################
# subroutine definitions

# Use Log::TraceMessages if installed.
BEGIN {
    eval { require Log::TraceMessages };
    if ($@) {
	*t = sub {};
	*d = sub { '' };
    }
    else {
#	\&Log::TraceMessages::Logfile = 'tv_grab_ar.log';
#	Log::TraceMessages::Logfile = 'tv_grab_ar.log';
	*t = \&Log::TraceMessages::t;
	*d = \&Log::TraceMessages::d;
	Log::TraceMessages::check_argv();
    }
}

####
# process_table: fetch a URL and process it
#
# arguments:
#    Date::Manip object giving the day to grab
#    xmltv id of channel
#    id of channel from cablevision
#
# returns: list of the programme hashes to write
#
sub process_table {
    my ($date, $ch_xmltv_id, $ch_ar_id) = @_;
    my $weekNr = GetWeekNumber($date);
#initial setting
#    my $url = "http://www.buscador" . $CableCompany . ".com.ar/index.php?template=fgrilla_semanal.tpl&canal=$ch_xmltv_id&semana=$weekNr";

#    my $url_head = "http://www.buscador" . $CableCompany . ".com.ar/index.php?#template=main_grilla_semanal.tpl&canal=$ch_xmltv_id&cantidadPasa=$weekNr";

    my $url_head = $urlRoot . "dinamicas/grilla_semanal/index.php?canal=$ch_xmltv_id&sintonia=$ch_ar_id&cantidadPasa=$weekNr";


#updated config
#    my $url = "http://www.buscador" . $CableCompany . ".com.ar/index.php?template=main_grilla_semanal.tpl&canal=$ch_xmltv_id&cantidadPasa=$weekNr";

    my $tree_head;

    eval {	
	 $tree_head = get_nice_tree $url_head;
	};
		
   if (not defined $tree_head) {
	warn "Unable to retrieve '$url_head'. This listing will be incomplete.";
	exit;
    } 
    my $tree=$tree_head->look_down(
        '_tag', 'div',sub {
            defined $_[0]->attr('id') and $_[0]->attr('id')=~ /grillasemanal/i
        });




    t $url_head;
    local $SIG{__WARN__} = sub {
#	my @timeData = localtime(time);
#	print join(' ', @timeData);
#        warn join(' ', @timeData) .  " $url_head: $_[0]";
        warn "$url_head: $_[0]";
    };

    # Need to disable redirects to work
    $XMLTV::Get_nice::ua->max_redirect(0);
    $XMLTV::Get_nice::ua->max_redirect(0);
    $XMLTV::Get_nice::ua->agent("");
#    $XMLTV::Get_nice::ua->user_agent("MSIE 6.0");
#    $XMLTV::Get_nice::ua->user_agent="MSIE 6.0";
    # parse the page to a document object


# The new Page holds a grid where the days are in columns and the hours are the rows
# The First days of the week is always Monday

# Variable @WeekDays will hold all programs for the week
	my @WeekDays;
#	my @Divs = $tree_head->find_by_tag_name("_tag"=>"div");

    my $tree2=$tree_head->look_down(
        '_tag', 'div',sub {
            defined $_[0]->attr('id') and $_[0]->attr('id')=~ /ContGrillaSemanal/i
        });
	my @Divs = $tree2->find_by_tag_name("_tag"=>"div");

	foreach my $elem (@Divs) {
	        my $cclass = $elem->attr('class');
        	if ((defined($cclass) && $cclass ne "") && (($cclass eq "diaSemana") || ($cclass eq "diaSemana par"))) {
        	  # @Programs =+ ($elem->find_by_tag_name("_tag"=>"a"));
		push (@WeekDays, $elem);       	           	    
		}
	}


# Variable @program_data will hold the programming for a single day
#    my @program_data = get_program_data($date,$tree,$tree_head,$ch_xmltv_id);
    my @program_data = get_program_data($date,@WeekDays);






    
    my @r;
    while (@program_data) {
        my $cur = shift @program_data;
        my $next = shift @program_data;
        unshift @program_data,$next if $next;
        my $cur_stop=ParseDate("$date $cur->{stoptime}");
        my $next_start;
        if ($next){
            $next_start=ParseDate("$date $next->{time}");
            $cur_stop=adjust_stoptime($date,$cur,$cur_stop,$next_start,$ch_xmltv_id);
        }
        if ( ($next and ( Date_Cmp($cur_stop,$next_start) <= 0 )) or !$next ){
            my $p = make_programme_hash($date, "$ch_xmltv_id.cablevision", $ch_ar_id, $cur);
            if (not $p) {
                require Data::Dumper;
                my $d = Data::Dumper::Dumper($cur);
                warn "cannot write programme on $ch_xmltv_id.cablevision on $date:\n$d\n";
            }
            else {
                push @r, $p;
            }
        }else {
                #warn "Detected duplicate programme on $ch_xmltv_id.cablevision date $date:\n\t$cur->{title} Start: $cur->{time} Ends: $cur->{stoptime} <=== Not writting\n\t$next->{title} Start: $next->{time} Ends: $next->{stoptime}\n";
        }
        $cur=undef;
        $next=undef;
    }
    @program_data=undef;
    return @r;
}

# Hashes the program for future saving in XMLTV format
sub make_programme_hash {
    my ($date, $ch_xmltv_id, $ch_ar_id, $cur ) = @_;

    my %prog;
    my @tempstop;
    $prog{channel}=$ch_xmltv_id;
    $prog{title}=[ [ $cur->{title}, $LANG ] ];
    t "turning local time $cur->{time}, on date $date, into UTC";
    eval { $prog{start}=to_UTC("$date $cur->{time}", '-0300') };
#	print "turning local time $cur->{time}, on date $date, into UTC is $prog{start}\n";
    if ($@) {
        warn "bad time string: $cur->{time}";
        return undef;
    }
    t "...got $prog{start}";
    eval { $prog{stop}=to_UTC("$date $cur->{stoptime}", '-0300') };
    if ($@) {
        warn "bad time string: $cur->{stoptime}";
        return undef;
    }
    t "...got $prog{stop}";

    if ($prog{stop} le $prog{start}) {
   	t "... stop Date wrong: [$prog{stop}] ";
	@tempstop=split(" ",$prog{stop});
	$tempstop[0]=DateCalc($tempstop[0], '+ 1 day');
	$tempstop[0]=~ s/://g;
	$prog{stop}= $tempstop[0] . " " . $tempstop[1];
    	t "...Fixed as $prog{stop}\n";
    } 
    $prog{desc}=[ [ $cur->{desc}, $LANG ] ] if defined $cur->{desc};
    $prog{credits}=$cur->{credits} if defined $cur->{credits};

#    $prog{credits}=[ "actor","pepe badia" ];

    $prog{category}=[ [ $cur->{category}, $LANG ] ] if defined $cur->{category};
    $prog{length}=$cur->{length} if defined $cur->{length};
    $prog{date}=[ [ $cur->{date}, $LANG ] ] if defined $cur->{date};
    $prog{country}=[ [ $cur->{country}, $LANG ] ] if defined $cur->{country};
    $prog{rating}=[ [ $cur->{rating}, $LANG ] ] if defined $cur->{rating};
    return \%prog;

}

sub to_UTC {
	my ($mydate,$UTCDelta) = @_;
#	my @tempdate=split(" ",$UTCDelta);
	my ($DCDelta);
	t "In time [$mydate][$UTCDelta]";
#	if (defined $UTCDelta) {
	if ($UTCDelta =~ /^([+-])(\d{2})(\d{2})$/) {
#		$DCDelta = $1 . " ";
		if ($1 eq '+') {
			$DCDelta = "- ";
		} else {
			$DCDelta = "+ ";
		}
		$DCDelta .= ($2 . " hours") if $2 > 0;
		$DCDelta .= ($3 . " minutes") if $3 > 0;		
		if (length($DCDelta) > 2) {
			$mydate=DateCalc($mydate, $DCDelta);
			$mydate=~ s/://g;
		}
	}
	t " ... out time [$mydate][+0000]\n";	
	return utc_offset("$mydate", '+0000');
}


# Returns an array of programs for the specified day in the global variable now.
# All work is done by private get_programs_for function



sub get_program_data {
    my ($date,@ProgramWeek) = @_;
    my @nowarr=(
        UnixDate($date,"%Y"),
        UnixDate($date,"%m"),
        UnixDate($date,"%d"));
    my $weekday=Date_DayOfWeek($nowarr[1],$nowarr[2],$nowarr[0])-1;

#    return () if  $offset_left < 0 or $offset_top < 0 or $height < 0;

    return get_programs_for($ProgramWeek[$weekday]);
}





# get channel listing
sub get_channels {
    my $bar = new XMLTV::ProgressBar('getting list of channels', 1)
	if not $opt_quiet;
    my %channels;
    my ($locationid) = ($location{id});
    my $url=$urlRoot . "popUpGuiaCanales.php?companiaSeleccionada=" . $locationid;
    t $url;
    # Need to disable redirects to work
    $XMLTV::Get_nice::ua->max_redirect(0);
    my $tree = get_nice_tree $url; #($url, \&decode_utf8);
#    my @menus =  $tree->find("./div[\@class='grilla_tv']");
    my @lista = $tree->find_by_tag_name("_tag"=>"li");
    foreach my $elem (@lista) {
	my $tempdiv= $elem->look_down("_tag"=>"div");
        my $channel_name=trim($tempdiv->as_text);
        if (defined($tempdiv) && $tempdiv ne "") {
            my $opt = $elem->look_down("_tag"=>"a");
            if (not $opt->attr('onclick') eq "") {
		my ($channel_id) = ($opt->attr('onclick') =~ /.*canal=(\d+).*/i);
                my ($channel_num) = ($opt->attr('onclick') =~ /.*sintonia=(\d+).*/i);
                $channels{$channel_id}=$channel_name;
		my $coded_chan_name=encode("UTF-8",$channel_name);
#		print $coded_chan_name;
                push @ch_all, { 
                              'display-name' => [[ $coded_chan_name, $LANG ],[$channel_num]],
                              'channel-num' => $channel_num  ,
#			      'channel-id'  => $channel_id,
                              'id'=> "$channel_id.cablevision" 
				};
            }
       }
    }
    die "no channels could be found" if not keys %channels;
    update $bar if not $opt_quiet;
    $bar->finish() if not $opt_quiet;
    return %channels;
}


sub get_txt_elems {
    my ($tree) = @_;

    my @txt_elem;
    my @txt_cont = $tree->look_down(
                        sub { ($_[0]->descendants() eq 0  ) },       
			sub { defined($_[0]->attr ("_content") ) } );
	foreach my $txt (@txt_cont) {
        	my @children=$txt->content_list;
		if (defined($children[0])) {
                  for (my $tmp=$children[0]) {
			s/^\s+//;s/\s+$//;
			push @txt_elem, $_;
                      }
                }
	}
    return @txt_elem;
}


# Bump a YYYYMMDD date by one.
sub nextday {
    my $d = shift;
    my $p = parse_date($d);
    my $n = DateCalc($p, '+ 1 day');
    return UnixDate($n, '%Q');
}

sub GetWeekNumber {
    my $date=shift;
    my $today=UnixDate(parse_date('now'),"%Q"); 
    
    my @datearr=(
        UnixDate($date,"%Y"),
        UnixDate($date,"%m"),
        UnixDate($date,"%d"));

    my @todayarr=(
        UnixDate($today,"%Y"),
        UnixDate($today,"%m"),
        UnixDate($today,"%d"));
    my $weekdate=Date_WeekOfYear($datearr[1],$datearr[2],$datearr[0],1);
    my $weektoday=Date_WeekOfYear($todayarr[1],$todayarr[2],$todayarr[0],1);
    if ($datearr[0]==$todayarr[0]){
        return $weekdate - $weektoday; 
    }
    return $weekdate; 
}


sub bump_start_day {
    my ($cur,$next) = @_;
    if (!defined($next)) {
	return undef;
    }
    my $start = UnixDate($cur->{time},'%H:%M');
    my $stop = UnixDate($next->{time},'%H:%M');
    if (Date_Cmp($start,$stop)>0) {
	return 1;
    } else {
	return 0;
    }
}
################################################################################
# 
# Because cablevision uses a grid made of DIV containers, we detect programs by 
# looking for coordinates
#
################################################################################

# Get top most container
sub get_grid_offset_top {
    my ($tree) = @_;
    my $elem=$tree->look_down(
        '_tag', 'div',sub {
            defined $_[0]->attr('id') and $_[0]->attr('id')=~ /layerHora0/i
        });
    return -1 if not defined $elem;
    my ($top)=($elem->attr('style') =~ /top:\s*(\d+)\s*(px|)/i);
    return $top; 
    return 0;
}

# Get left most offset for containers
sub get_grid_offset_left {
    my ($tree) = @_;
    my $elem=$tree->look_down(
        '_tag', 'div',sub {
            defined $_[0]->attr('id') and $_[0]->attr('id')=~ /layerHora0/i
        });
    return -1 if not defined $elem;
    my ($left)=($elem->attr('style') =~ /width:\s*(\d+)\s*(px|)/i);
    return $left;
}

# Get default row height
sub get_grid_row_height {
    my ($tree) = @_;
    my $elem=$tree->look_down(
        '_tag', 'div',sub {
            defined $_[0]->attr('id') and $_[0]->attr('id')=~ /layerHora0/i
        });
    return -1 if not defined $elem;
    my ($height)=($elem->attr('style') =~ /height:\s*(\d+)\s*(px|)/i);
    return $height;
}

# Get default row width
sub get_grid_row_width {
    my ($tree) = shift;
    my @rowDia=$tree->look_down(
        '_tag', 'td',sub {
            defined $_[0]->attr('class') && $_[0]->attr('class')=~ /layerDia/i
        });
    return $rowDia[int ($#rowDia/2)]->attr('width');
}








sub parse_program {
		my ($celda) = @_; 
#channel, station, startday, celda)

# <a href="ficha.php?verFicha=224193" rel="#overlay_ficha" class="programaSemana" title="Parental Control" style="height:34px; top:-1px">

	my $title= encode("UTF-8",trim($celda->attr('title')));
	if (! (defined $title)) {
		$title= encode("UTF-8",trim($celda->as_text));         
	} 
        my ($top)=($celda->attr('style') =~ /top:\s*(-{0,1}\d+)\s*(px|)/i);
        my ($left)=($celda->attr('style') =~ /left:\s*(-{0,1}\d+)\s*(px|)/i);
        my ($height)=($celda->attr('style') =~ /height:\s*(-{0,1}\d+)\s*(px|)/i);
        my ($width)=($celda->attr('style') =~ /width:\s*(-{0,1}\d+)\s*(px|)/i);
	my $start=int (($top + 1) * (3600 / 68));

	my $mins=int($start/60);
	if ( $mins % 5 !=0 ){#Adjust to a multiple of 5 
        	$mins=($mins+5)-(($mins+5)%5); 
	}
	my $startT = UnixDate(DateCalc("00:00","+ $mins minutes"),"%H:%M");

	my $length = int(($height) * (3600 / 68));
	$mins=int($length/60);
	if ( $mins % 5 !=0 ){#Adjust to a multiple of 5 
        	$mins=($mins+5)-(($mins+5)%5); 
	}
	my $LengthT = UnixDate(DateCalc("00:00","+ $mins minutes"),"%H:%M");
	my $endT = UnixDate(DateCalc($startT,"+ $mins minutes"),"%H:%M");

        my %ret;

           %ret = (
                time =>         $startT,
                stoptime =>     $endT,
                title=>         $title
		);
	if ($opt_get_details) {
		my %TempDesc;
		my ($progindex)=($celda->attr('href') =~ /ficha\.php\?verFicha.(\d+)/i);
		if (exists $Descriptions{$progindex}) {
			#original format
			#warn "\nfound index: $progindex with description [" . $Descriptions{$progindex} . "]\n\n";
			my $tempref = ($Descriptions{$progindex}) ;
			%TempDesc= %$tempref;
			foreach my $keyvalue (sort keys %TempDesc) {
				if (($keyvalue ne 'time') && ($keyvalue ne 'stoptime')) {
					$ret{$keyvalue} = $TempDesc{$keyvalue}
				}
			}
		}
		else {
			#original format
			eval {
				warn "\n" . $urlRoot . "ficha.php?verFicha=$progindex\n";
				my $ficha_tree=get_nice_tree($urlRoot . "ficha.php?verFicha=$progindex");
#			};
#			eval {
				my $Categoria = ($ficha_tree->look_down('_tag','h1'));
				if (defined $Categoria) {
					$Categoria = encode("UTF-8",trim($Categoria->as_text));
				} else {
					$Categoria = "";
				}
#			};
#			eval {
				my $FichaBox=$ficha_tree->look_down(
			       		'_tag', 'div',sub {
					    defined $_[0]->attr('id') and
				            $_[0]->attr('id')=~ /ficha/i
				        });
#			};
#			eval {
				my $TituloEsp = ($ficha_tree->look_down('_tag','h2'));
				if (defined $TituloEsp) {
					$TituloEsp = encode("UTF-8",trim($TituloEsp->as_text));
				} else {
					$TituloEsp = $title;
				}
#			};
#			eval {
				my $SinopsisBox=$ficha_tree->look_down(
			       		'_tag', 'div',sub {
					    defined $_[0]->attr('class') and
				            $_[0]->attr('class')=~ /sinopsis/i
				        });
				my $sinopsis= ($SinopsisBox->look_down('_tag','p')) if (defined $SinopsisBox); 
				if (defined $sinopsis) {
					$sinopsis=encode("UTF-8",trim($sinopsis->as_text));
				} else {
					$sinopsis = "";
				}
	        		$ret{desc} = $sinopsis;
#			};
#			eval {			
	  		   	my %credits;
  				my @FichaTecnica = ($ficha_tree->look_down('_tag','li'));
				my $PropertyField;
				foreach my $Propiedad (@FichaTecnica){				
					if ((defined $Propiedad->attr('class')) and ($Propiedad->attr('class')=~ /itemf1/i)) {	
						$PropertyField=trim($Propiedad->as_text);
					} 
					else {
						my $valor;
						$valor=encode("UTF-8",trim($Propiedad->as_text));
						if ($PropertyField =~ m/Protagonista((s)?)/i ) {
							$valor =~ s/, /,/g;
							my @algo =split(',', $valor);
							$credits{actor} = \@algo
						}
						elsif ($PropertyField =~ m/Director((es)?)/i) { 
							$valor =~ s/, /,/g;
							my @algo =split(',', $valor);
							$credits{director} = \@algo
						}
						elsif ($PropertyField =~ m/Conductor((es)?)/i) { 
							$valor =~ s/, /,/g;
							my @algo =split(',', $valor);
							$credits{presenter} = \@algo
						}
						elsif ($PropertyField =~ m/G.nero/i) { 
							$ret{category} = $valor;
						}
						elsif ($PropertyField =~ m/(Duraci&oacute;n|Duración)/) { # html, utf-8 bytes, missing unicode chars
							$valor =~ /(\d*)\s(.*)(\n|\r)?/i;
							my $TempLength=$1;
							my $tempunit=$2;
							$tempunit=~ s/^\s//;
							$tempunit=~ s/\s$//;
#							$tempunit=~ s/minutos?/minutes/;
#							$tempunit=~ s/horas?/hours/;
#							$tempunit=~ s/segundos?/seconds/;
#							if ($tempunit=~ s/minutos?/i) { $TempLength = ($TempLength *60) }	
#							if ($tempunit=~ s/horas?/i) { $TempLength = ($TempLength *60) }	
							$ret{length} = ($TempLength *60);
						}
						elsif ($PropertyField =~ m/Pa.s/i) { 
							$ret{country} = $valor;
						}
						elsif ($PropertyField =~ m/(A&ntilde;o|Ano)/) { 
							$ret{date} = $valor;
						}
						elsif ($PropertyField eq 'Título original') { 
							$ret{title2} = $valor;
						}
						elsif ($PropertyField =~ m/Clasificaci.n/i) { 
							$ret{rating} = $valor;
						}
						else { 
							print STDERR "ignoring useless data ... [$PropertyField]\n";
						}
					$PropertyField = '';
					}
				}
				$ret{credits} = \%credits if %credits;
			};
		}	
		$Descriptions{$progindex}=\%ret;
	}

	return \%ret;

}


################################################################################
# 
# Returns a hash with:
#   time
#   title
#   description
# 
################################################################################
sub get_programs_for {
    my ($ProgramDay)=@_;
    my @data;
#    my @row=$tree->look_down(
#        '_tag', 'div',sub {
#            $_[0]->attr('id')=~ /layer\d{1,2}/i and
#            $_[0]->attr('class')=~ /layerPrograma/i and 
#            defined $_[0]->attr('style') 
#        });



	my @Programs = $ProgramDay->find_by_tag_name("_tag"=>"a");       	           	    

#	print @Programs;
	foreach my $programa (@Programs) {
		push @data, parse_program ($programa)
	}


    return sort { Date_Cmp(ParseDate($a->{time}),ParseDate($b->{time})) } @data;
}

















# Returns the time based on coordinates
sub get_start_time {
 my ($curr_top,$offset_top,$default_height)=@_;
    # default_height ===> 30 mins
    # curr_top - offset top ==> (curr_top-offset_top)*30/default_height
 my $mins=int (($curr_top-$offset_top)*30/$default_height);
    return "24:00" if $mins >= 1440;
    if ( $mins % 5 !=0 ){#Adjust to a multiple of 5 
        $mins=($mins+5)-(($mins+5)%5); 
    }
    return UnixDate(DateCalc("00:00","+ $mins minutes"),"%H:%M");
}

# Remove empty spaces from string
sub trim {
    my $string = shift;
    $string =~ s/^\s+//;
    $string =~ s/\s+$//;
    return $string;
}

# Remove HTML tagging from extended chars
sub Clean_amp {
    my ($texto)=@_;
	Encode::from_to($texto, "iso-8859-1", "utf8");
	my $textoold=$texto;
	$texto =~ s/&amp;/&/g;
	$texto =~ s/&acute;/á/g;
	$texto =~ s/&aacute;/á/g;
	$texto =~ s/&eacute;/é/g;
	$texto =~ s/&iacute;/í/g;
	$texto =~ s/&oacute;/ó/g;
	$texto =~ s/&uacute;/ú/g;
	$texto =~ s/&ntilde;/ñ/g;


   return $texto;
}

# Adjusts stop time from a program to a time that doesn't overlaps considering a threshold
sub adjust_stoptime {
    my ($date,$prog_cur,$curstop,$nextstart,$ch_xmltv_id)=@_;
    if ( Date_Cmp($curstop,$nextstart) > 0 ){
        my $secscur=UnixDate($curstop,"%o");
        my $secsnext=UnixDate($nextstart,"%o");
        my $delta=$secscur-$secsnext;
        if ( $delta <= $allowed_overlap_tresh ){
            my $before=$prog_cur->{stoptime};
            $prog_cur->{stoptime}=$nextstart;
            my $after=$prog_cur->{stoptime};
            #warn "Correcting stop time for programme $prog_cur->{title} on $ch_xmltv_id.cablevision date $date:\n\tFrom $before to $after";
            $curstop=ParseDate("$date $prog_cur->{stoptime}");
        }
    }
    return $curstop;
}



# Select Location
sub select_Location( ) {

    my $bar = new XMLTV::ProgressBar('getting list of Locations', 1)
	if not $opt_quiet;


    my $tree = get_nice_tree($urlRoot . "/popUpSeleccionZona.php?paisSeleccionado=cd");
    my @options=$tree->look_down(
        '_tag'=>'a', sub {
            defined $_[0]->attr('href') and
            ($_[0]->attr('href') =~ /CambiarZona/i)
        }
    );
    my %Locations;
	# Hardcoding Values for other companies
	$Locations{"* Telecentro *"} = 17;
	$Locations{"* DirecTV *"} = 16;
	$Locations{"* Capital Federal y GBA *"} = 3;

    foreach (@options){
	my $LocationID=$_->attr('href');
	$LocationID=~ s/\?cambiarzona=//i;
        $Locations{$_->as_text()}=$LocationID;
	
    }
    update $bar if not $opt_quiet;
    $bar->finish() if not $opt_quiet;

    my @names= sort keys %Locations;
    my $choice=ask_choice("Select your Locations:",$names[0],@names);
    return ( id=> $Locations{$choice}, name=>$choice );

}

